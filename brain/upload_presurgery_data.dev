#!/usr/bin/perl -w

#   $URL: https://intrarad.ucsf.edu/svn/rad_software/surbeck/brain/DB/nelson_ncvi/trunk/data_upload_qr_scripts/upload_presurgery_data $
#   $Rev: 31671 $
#   $Author: jasonc@RADIOLOGY.UCSF.EDU $
#   $Date: 2014-05-21 10:02:56 -0700 (Wed, 21 May 2014) $
#
#   Specialized data upload script to parse OnCore "Neuro_Surgery_Report.xls" and upload clinical diagnosis data to 
#   brain DB. 
#

use strict;
use DBI;
use DBD::Pg;
use Getopt::Long;
Getopt::Long::config("bundling");
use Pod::Usage;
use File::Spec;
use Date::Parse;
use Cwd;
use Cwd 'abs_path';
require SET_BRAIN_MODULES;
use Spreadsheet::ParseExcel;

##########################################################################
#
#  These constants will be used as the keys to a hash for each row in the 
#  spreadsheet. Each key is a column.
#
##########################################################################
use constant BRAIN_ID_KEY                     => "brain_id";
use constant EXAM_ID_KEY                      => "exam_id";
use constant SF_NUMBER_KEY                    => "sf_number";
use constant MRN_KEY                          => "mrn";
use constant BIRTH_DATE_KEY                   => "birth_date";
use constant RACE_KEY                         => "race";
use constant ETHNICITY_KEY                    => "ethnicity";
use constant GENDER_KEY                       => "gender";
use constant CURRENT_SURGERY_DATE_KEY         => "current_surgery_date";
use constant CURRENT_SURGEON_KEY              => "current_surgeon";
use constant CURRENT_GRADE_KEY                => "current_grade";
use constant CURRENT_HIST_TYPE_KEY            => "current_hist_type";
use constant CURRENT_EXTENT_OF_RESECTION_KEY  => "current_extent_of_resection";
use constant CURRENT_SURGERY_NAVIGATION_KEY   => "current_surgery_navigation";
use constant CURRENT_SURGERY_SCREENSHOTS_KEY  => "current_surgery_screenshots";
use constant CURRENT_SURGERY_COMMENTS_KEY     => "current_surgery_comments";
use constant CURRENT_DIAGNOSIS_COMMENTS_KEY   => "current_diagnosis_comments";
use constant ORIGINAL_SURGERY_DATE_KEY        => "original_surgery_date";
use constant ORIGINAL_GRADE_KEY               => "original_grade";
use constant ORIGINAL_HIST_TYPE_KEY           => "original_hist_type";
use constant ORIGINAL_EXTENT_OF_RESECTION_KEY => "original_extent_of_resection";
use constant ORIGINAL_SURGERY_COMMENTS_KEY    => "original_surgery_comments";
use constant ORIGINAL_DIAGNOSIS_COMMENTS_KEY  => "original_diagnosis_comments";

sub print_row( $ );
sub get_hash_key( $ );
sub update_subject_info( $ );
sub upload_current_diagnosis( $ );
sub upload_prior_diagnosis( $ );
sub upload_surgery_acquisition_info( $ );
sub execute_diagnosis_script( $$$$$$$$$$ );
sub trim( $ );

my $script    = $0;
our $dbh; 
our $xl_row; 
our $xl_col; 

GetOptions(
    "f=s"             => \my $filename,
    "d"               => \my $debug,
    "current_surgery" => \my $current_surgery,
    "prior_surgery"   => \my $prior_surgery,
    "acquisition"     => \my $acquisition,
    "subject_info"    => \my $subject_info,
    "c"               => \my $commit
) or pod2usage(-verbose=>2, -exitval=>2);

if (!defined $filename ) {
    pod2usage(-verbose=>2, -exitval=>2);
}

if( $debug ) {
    BRAIN_DB_UTILS::dbutils_turn_debug_on();
}

########################## 
#   Connect to DB:
##########################
BRAIN_DB::brain_db_set_env( $script );
$dbh = BRAIN_DB::brain_db_get_connection();

my $parser   = Spreadsheet::ParseExcel->new();

# Lets define all the hash keys we are going to use

my $workbook = $parser->parse($filename);

if ( !defined $workbook ) {
    die $parser->error(), ".\n";
}
for my $worksheet ( $workbook->worksheet('Completed') ) {

    
    my ( $row_min, $row_max ) = $worksheet->row_range();
    my ( $col_min, $col_max ) = $worksheet->col_range();
    # We know the first row is the title, and the second is the headings.
    $row_min +=2; 

    # Some rows have identifiers, and some do not (tissue samples) 
    # We'll save the last identifying row for uploading sample data.
    my %last_fields_with_mrn = ();

    for $xl_row ( $row_min .. $row_max ) {
        my %fields = ();
        for $xl_col ( $col_min .. $col_max ) {
            my $cell    = $worksheet->get_cell( $xl_row, $xl_col );
            if( defined( $cell ) && $cell->value() ne "" ) {
                my $key = get_hash_key( $xl_col );
                if( $key ne "" ) {
                    $fields{$key} = trim($cell->value());
                } 
            } 
        }

        #If we have an MRN.
        if( !$fields{MRN_KEY()} ) {
            # Save identifying row
            $fields{MRN_KEY()} = $last_fields_with_mrn{MRN_KEY()};
        } else {
            if( $subject_info ) { 
                if( update_subject_info( \%fields ) == 1 ) {
                    print "ERROR: Failed to update subject info:\n";
                    print_row(\%fields);
                    #exit(1);
                } elsif( $commit ) {
                    print "SUCCESS: Updated patient info for mrn $fields{MRN_KEY()}.\n";
                }
            }
            %last_fields_with_mrn = %fields;
        }
        if( $current_surgery && $fields{CURRENT_SURGERY_DATE_KEY()} ) {
            if( upload_current_diagnosis(\%fields) == 1 ) {
                print "ERROR: Failed to upload current surgery info:\n";
                print_row(\%fields);
                #exit(1);
            } elsif($commit)  {
                print "SUCCESS: Inserted diagnosis for mrn $fields{MRN_KEY()} on $fields{CURRENT_SURGERY_DATE_KEY()}\n";
            }
        }
        if( $prior_surgery && $fields{ORIGINAL_SURGERY_DATE_KEY()}  ) {
            if( upload_prior_diagnosis(\%fields) == 1 ) {
                print "ERROR: Failed to upload original surgery info:\n";
                print_row(\%fields);
                #exit(1);
            } elsif($commit)  {
                print "SUCCESS: Inserted diagnosis for mrn $fields{MRN_KEY()} on $fields{ORIGINAL_SURGERY_DATE_KEY()}\n";
            }
        }
        if( $acquisition && $fields{SF_NUMBER_KEY()}  ) {
            if ( upload_surgery_acquisition_info(\%fields) == 1 ) {
                print "ERROR: Failed to upload original surgery info:\n";
                print_row(\%fields);
                exit(1);
            } elsif ($commit ) {
                print "SUCCESS: Inserted surgery acquisition info for sf $fields{SF_NUMBER_KEY()}\n";
            }
        }
    }
}

#
# Prints all the fields for the given hash.
#
sub print_row( $ )
{
    my %fields = %{$_[0]};
    print "*********************************************************************\n";
    while ((my $key, my $value) = each(%fields)){
         print "<$key -----> $value>\n";
    }
    print "*********************************************************************\n";


}


#
# Uploads prior diagnosis based on fields from the given row hash.
#
sub upload_prior_diagnosis( $ )
{
    my %fields = %{$_[0]};
    my $error = 0;
    my $surgeon = "";
    my $sfn = "";
    my $mrn = $fields{MRN_KEY()};
    my $tnum = "";

    # Lets get the dates from the date field
    my $date      = trim( $fields{ORIGINAL_SURGERY_DATE_KEY()} );
    my $grade     = trim( $fields{ORIGINAL_GRADE_KEY()} );
    my $hist_type = trim( $fields{ORIGINAL_HIST_TYPE_KEY()} );
    my $resection = $fields{ORIGINAL_EXTENT_OF_RESECTION_KEY()};
    
    return execute_diagnosis_script( $mrn, $tnum, $sfn, $date, $surgeon, $resection, $hist_type, $grade, "", "" );

}


#
# Uploads current diagnosis based on fields from the given row hash.
#
sub upload_current_diagnosis( $ )
{
    my %fields = %{$_[0]};
    my $error = 0;
    my $mrn                = $fields{MRN_KEY()};
    my $sfn                = $fields{SF_NUMBER_KEY()};
    my $date               = $fields{CURRENT_SURGERY_DATE_KEY()};
    my $surgeon            = trim($fields{CURRENT_SURGEON_KEY()});
    my $resection          = trim($fields{CURRENT_EXTENT_OF_RESECTION_KEY()});
    my $grade              = trim( $fields{CURRENT_GRADE_KEY()} );
    my $hist_type          = trim( $fields{CURRENT_HIST_TYPE_KEY()} );
    my $surgery_comments   = $fields{CURRENT_SURGERY_COMMENTS_KEY()};
    my $diagnosis_comments = $fields{CURRENT_DIAGNOSIS_COMMENTS_KEY()};

    # Lets extract the number from tnum;
    my $tnum               = $fields{EXAM_ID_KEY()};
    if( $tnum ) {
        if( $tnum =~ m/^t(\d*)$/ ) {
            $tnum = $1;
        } else {
            print "ERROR: Invalid entry for t-number: $tnum\n";
            return 1;
        }
    }
    
    if(  !$mrn || $mrn eq "" ) {
        print "ERROR: Missing mrn!\n";
        return 1;
    } elsif ( !$date || $date eq "" ) {
        print "ERROR: Missing surgery date!\n";
        return 1;
    } else {
        return execute_diagnosis_script( $mrn, $tnum, $sfn, $date, $surgeon, $resection, $hist_type, $grade, $surgery_comments, $diagnosis_comments );
    }

}


#
# Executes the insert_bt_diagnosis scripte for the given arguments.
#
sub execute_diagnosis_script( $$$$$$$$$$ ) 
{
    my ($mrn, $tnum, $sf_num, $date, $surgeon, $resection_extent, $hist_type, $hist_grade, $surgery_comments, $diagnosis_comments) = @_;

    my $cmd;
    my $insert_diagnosis_script    = "insert_bt_diagnosis_event";
    if ( $script =~ m/\.dev$/ ) {
        $insert_diagnosis_script .= ".dev";
    }
    #
    # Lets insert the diagnosis and surgery information
    #
    $cmd = "$insert_diagnosis_script --mrn " . $mrn;
    if( $sf_num && $sf_num ne "" ) {
        $cmd .= " --sfn $sf_num ";
    }
    if ( !BRAIN_DB_UTILS::dbutils_is_partial_date_valid( $date, 0 ) ) {
        print "\n\nERROR:  incorrect date format for diagnosis: $date\n\n";
        return 1;
    }

    $cmd .= " --date $date ";

    if( $tnum && $tnum ne "" ) {
        $cmd .= " --tnum $tnum ";
    }
    if( $surgeon && $surgeon ne "" ) {
        $cmd .= " --surgeon \"$surgeon\" ";
    }
    if( $hist_type && $hist_type ne "" ) {
        $cmd .= " --hist \"$hist_type\" ";
    }
    if( $hist_grade && $hist_grade ne "" ) {
        $cmd .= " --grade \"$hist_grade\" ";
    }
    if( $resection_extent && $resection_extent ne "" ) {
        $cmd .= " --resection_extent \"$resection_extent\" ";
    }
    if( $surgery_comments && $surgery_comments ne "" ) {
        $cmd .= " --surgery_comments \"$surgery_comments\" ";
    }
    if( $diagnosis_comments && $diagnosis_comments ne "" ) {
        $cmd .= " --diagnosis_comments \"$diagnosis_comments\" ";
    }
    if ( defined($commit) ) {
        if( system($cmd) ) {
            print "===========================================.\n\n";
            print "ERROR: failed running $cmd\n";
            print "===========================================.\n\n";
            return 1;
        }
    } else {
        print "Execute Command: $cmd\n";
    }
    return 0;
}


sub update_subject_info( $ )
{
    my %fields = %{$_[0]};

    my $mrn        = trim( $fields{MRN_KEY()} );
    my $birth_date = trim( $fields{BIRTH_DATE_KEY()} );
    my $race       = trim( $fields{RACE_KEY()} );
    my $ethnicity  = trim( $fields{ETHNICITY_KEY()} );
    my $gender     = trim( $fields{GENDER_KEY()} );

    if( $gender && $gender eq "Male" ) {
        $gender = "M";
    }
    if( $gender && $gender eq "Female" ) {
        $gender = "F";
    }
    

    my $cmd;
    my $update_subject_info    = "update_subject_info";
    if ( $script =~ m/\.dev$/ ) {
        $update_subject_info .= ".dev";
    }

    #
    # Lets insert the diagnosis and surgery information
    #
    $cmd = "$update_subject_info --mrn " . $mrn;

    if( $birth_date && $birth_date ne "" ) {
        if ( !BRAIN_DB_UTILS::dbutils_is_partial_date_valid( $birth_date, 1 ) ) {
            print "\n\nERROR:  incorrect date format for birthdate: $birth_date\n\n";
            return 1;
        }

        $cmd .= " --birth_date $birth_date ";
    }
    if( $gender && $gender ne "" ) {
        $cmd .= " --gender $gender ";
    }
    if( $race && $race ne "" ) {
        $cmd .= " --race '$race' ";
    }
    if( $ethnicity && $ethnicity ne "" ) {
        $cmd .= " --ethnicity '$ethnicity' ";
    }

    if ( defined($commit) ) {
        if( system($cmd) ) {
            print "===========================================.\n\n";
            print "ERROR: failed running $cmd\n";
            print "===========================================.\n\n";
            return 1;
        }
    } else {
        print "Execute Command: $cmd\n";
    }
     
    return 0;

}


sub upload_surgery_acquisition_info( $ )
{
    my %fields = %{$_[0]};

    my $sf_num      = trim( $fields{SF_NUMBER_KEY()} );
    my $navigation  = trim( $fields{CURRENT_SURGERY_NAVIGATION_KEY()} );
    my $screenshots = trim( $fields{CURRENT_SURGERY_SCREENSHOTS_KEY()} );
    if( !defined($sf_num) ) {
        print "=====================================================.\n\n";
        print "ERROR: failed updating acquisition info, no sf number.\n";
        print "=====================================================.\n\n";
        return 1;
    }

    if ( defined($commit) ) {
        if( BRAIN_DB_UTILS::dbutils_update_surgery_acquisition_info( $sf_num, $navigation, $screenshots, "" ) == -1  ) {
            print "=====================================================.\n\n";
            print "ERROR: failed updating acquisition info.              \n";
            print "=====================================================.\n\n";
            return 1;
        }
    } else {
            if( !defined( $sf_num ) ) {
                $sf_num = "null";
            }
            if( !defined( $navigation ) ) {
                $navigation = "null";
            }
            if( !defined( $screenshots ) ) {
                $screenshots = "null";
            }
            print "Executing dbutils_update_surgery_acquisition_info( $sf_num, $navigation, $screenshots, \"\" )\n";
    }
    return 0;
}


sub get_hash_key ( $ )
{

    my ($column_index) = @_; 
    my $key = "";
    if( $column_index == 1 ) {
        $key = BRAIN_ID_KEY; 
    } elsif ( $column_index == 2 ) {
        $key = EXAM_ID_KEY;
    } elsif ( $column_index == 3 ) {
        $key = SF_NUMBER_KEY;
    } elsif ( $column_index == 5 ) {
        $key = MRN_KEY;
    } elsif ( $column_index == 6 ) {
        $key = BIRTH_DATE_KEY;
    } elsif ( $column_index == 7 ) {
        $key = RACE_KEY;
    } elsif ( $column_index == 8 ) {
        $key = ETHNICITY_KEY;
    } elsif ( $column_index == 11 ) {
        $key = GENDER_KEY;
    } elsif ( $column_index == 12 ) {
        $key = CURRENT_SURGERY_DATE_KEY;
    } elsif ( $column_index == 13 ) {
        $key = CURRENT_SURGEON_KEY;
    } elsif ( $column_index == 14 ) {
        $key = CURRENT_GRADE_KEY;
    } elsif ( $column_index == 15 ) {
        $key = CURRENT_HIST_TYPE_KEY;
    } elsif ( $column_index == 19 ) {
        $key = CURRENT_EXTENT_OF_RESECTION_KEY;
    } elsif ( $column_index == 26 ) {
        $key = CURRENT_SURGERY_NAVIGATION_KEY;
    } elsif ( $column_index == 28 ) {
        $key = CURRENT_SURGERY_SCREENSHOTS_KEY;
    } elsif ( $column_index == 30 ) {
        $key = ORIGINAL_SURGERY_DATE_KEY;
    } elsif ( $column_index == 31 ) {
        $key = ORIGINAL_GRADE_KEY;
    } elsif ( $column_index == 32 ) {
        $key = ORIGINAL_HIST_TYPE_KEY;
    } elsif ( $column_index == 35 ) {
        $key = ORIGINAL_EXTENT_OF_RESECTION_KEY;
    } elsif ( $column_index == 187  ) {
        $key = CURRENT_SURGERY_COMMENTS_KEY;
    } elsif ( $column_index == 188 ) {
        $key = ORIGINAL_SURGERY_COMMENTS_KEY;
    } elsif ( $column_index == 189  ) {
        $key = ORIGINAL_DIAGNOSIS_COMMENTS_KEY;
    } elsif ( $column_index == 190  ) {
        $key = CURRENT_DIAGNOSIS_COMMENTS_KEY;
    }
    return $key; 
}

sub trim ( $ )
{
    my( $string ) = @_;
    if( $string ) {
        $string =~ s/^\s+//;
        $string =~ s/\s+$//;
    }
    return $string;
}
###############################################################################
#
#   POD Usage docs
#
###############################################################################

=head1 NAME

upload_presurgery_data

=head1 SYNOPSIS

upload_presurgery_data [-c] [-d] -f filename 

        -f              filename        the filename of the spreadsheet to upload 
        -c                              commit data           
        --current_surgery               upload current surgery data 
        --prior_surgery                 upload prior surgery data 
        --acquisition                   upload acquisition data 
        --subject_info                  upload subject info 

=head1 DESCRIPTION

Program uploads presurgery data to the brain DB. 


=cut

